// Substitute the jitpack SDK dependency for your local instance of the project
// Enabling here allows you to develop against a local android SDK for kotlin files in the library module
if (file("local.properties").canRead()) {
    def localProperties = new Properties()
    localProperties.load(new FileInputStream(file("local.properties")))
    localCompositeBuild = localProperties["localCompositeBuild"] ?: localCompositeBuild
    localSdkPath = localProperties["localSdkPath"] ?: localSdkPath
}

if (localCompositeBuild == "true") {
    def sdkDirectory = new File(localSdkPath)

    if (!sdkDirectory.exists()) {
        throw new GradleException("Local Klaviyo Android SDK directory does not exist: " + sdkDirectory.getAbsolutePath())
    } else {
        print("Configuring composite project with local Klaviyo Android SDK for library.")
    }

    includeBuild(sdkDirectory) {
        dependencySubstitution {
            // substitute remote dependency with local module
            substitute module("com.github.klaviyo.klaviyo-android-sdk:core") using project(":sdk:core")
            substitute module("com.github.klaviyo.klaviyo-android-sdk:analytics") using project(":sdk:analytics")
            substitute module("com.github.klaviyo.klaviyo-android-sdk:forms") using project(":sdk:forms")
            substitute module("com.github.klaviyo.klaviyo-android-sdk:push-fcm") using project(":sdk:push-fcm")
        }
    }
}